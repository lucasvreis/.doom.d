:PROPERTIES:
:ID:       677d2cee-3665-43f4-a895-6fe08f11eb9a
:END:
#+title: Minha Configura√ß√£o do Emacs
#+property: header-args :mkdirp yes

Vamos usar o binding l√©xico:
#+begin_src emacs-lisp
;;; -*- lexical-binding: t; -*-
#+end_src

* Est√©tico
** Tema
#+begin_src emacs-lisp
(setq doom-theme 'doom-moonlight)
#+end_src

*** Esconder temas

Deixa a sele√ß√£o menos distrativa, removendo os temas n√£o-doom e os temas da
lista abaixo.

#+begin_src emacs-lisp
(defvar my/custom-themes-exclude
  '(doom-acario-light
    doom-acario-dark
    doom-badger
    doom-dark+
    doom-henna
    doom-homage-white
    doom-Iosvkem
    doom-1337
    doom-molokai
    doom-sourcerer
    doom-peacock
    doom-wilmersdorf
    doom-manegarm
    doom-ephemeral
    doom-nova
    doom-opera
    doom-zenburn
    doom-ayu-mirage
    doom-vibrant
    doom-ir-black
    doom-old-hope
    doom-miramare
    doom-monokai-spectrum
    doom-monokai-ristretto))

(advice-add 'custom-available-themes :filter-return
            (lambda (l)
              (seq-difference
               (seq-filter (lambda (x) (s-prefix? "doom-" (symbol-name x))) l)
               my/custom-themes-exclude #'eq)))
#+end_src

** Faces
*** Fontes

#+begin_src emacs-lisp :results none
(setq doom-font                (font-spec :family "VictorMono Nerd Font Mono" :size 20 :weight 'normal)
      doom-variable-pitch-font (font-spec :family "Overpass" :size 19 :weight 'light)
      doom-serif-font          (font-spec :family "IBM Plex Mono" :weight 'light))
      ;; doom-unicode-font        (font-spec :family "JuliaMono" :weight 'normal))

;; Colocamos uma ordem de prioridade para tentar ter todos os unicodes e emojis.
(setq use-default-font-for-symbols t)
(defun my/adjust-fonts ()
  (set-fontset-font t 'unicode (font-spec :family "JuliaMono" :weight 'light))
  (set-fontset-font t 'unicode "Twemoji" nil 'append))

(add-hook! 'after-setting-font-hook #'my/adjust-fonts)
#+end_src

*** C√≥digo (prog)
Deixamos os coment√°rios it√°licas, e os ~keywords~ obl√≠quos.

#+begin_src emacs-lisp
(custom-set-faces!
  '(font-lock-comment-face :slant italic)
  '(font-lock-keyword-face :slant oblique))
#+end_src

*** Yasnippet
#+begin_src emacs-lisp
(custom-set-faces!
  `(yas-field-highlight-face
    :inherit nil
    :background ,(doom-blend "#b315b3" (doom-color 'bg) 0.4)
    :foreground "undefined"))
#+end_src
*** Org
#+begin_src emacs-lisp :results none
(custom-set-faces!
  `(org-latex-and-related :weight normal)
  `(font-latex-math-face :inherit org-latex-and-related :foreground ,(doom-color 'fg))
  '(org-block-begin-line :extend t))
(custom-theme-set-faces! 'doom-flatwhite
  `(org-latex-and-related :foreground nil :background ,(doom-color 'fw-green-blend)))
(custom-set-faces!
  '(outline-1 :weight extra-bold)
  '(outline-2 :weight bold)
  '(outline-3 :weight bold)
  '(outline-4 :weight semi-bold)
  '(outline-5 :weight semi-bold)
  '(outline-6 :weight semi-bold)
  '(outline-8 :weight semi-bold)
  '(outline-9 :weight semi-bold))
#+end_src

No terminal, a fonte =org-hide= n√£o funciona bem.
#+begin_src emacs-lisp
(custom-set-faces!
  `(org-hide :foreground ,(doom-color 'bg)))
#+end_src

*** √çcones
Ajusta o tamanho dos √≠cones.

#+begin_src emacs-lisp
(setq all-the-icons-scale-factor 0.88)
#+end_src

*** Fixes
O tema =doom-rouge= n√£o tem um =hl-line= distingu√≠vel...
#+begin_src emacs-lisp
(custom-theme-set-faces! 'doom-rouge
  '(hl-line :background "#171727"))
#+end_src
** Modeline
*** Faces
#+begin_src emacs-lisp
(custom-set-faces!
  '(mode-line :height 110 :family "JuliaMono")
  '(mode-line-inactive :height 110 :family "JuliaMono")
  '(doom-modeline-buffer-modified :foreground "#c63")
  '(doom-modeline-info :foreground "white"))
(setq! +modeline-height 26)
#+end_src

*** Doom-modeline
#+begin_src emacs-lisp
(setq doom-modeline-irc nil
      doom-modeline-icon nil)
#+end_src

#+begin_src elisp :tangle tangle/minor/doom-modeline.el
;;; tangle/minor/doom-modeline.el -*- lexical-binding: t; -*-
(doom-modeline-def-segment buffer-name
  "Display the current buffer's name, without any other information."
  (concat
    (doom-modeline-spc)
    (doom-modeline--buffer-name)))

(doom-modeline-def-segment pdf-icon
  "PDF icon from all-the-icons."
  (concat
    (doom-modeline-spc)
    (doom-modeline-icon 'octicon "file-pdf" nil nil
                        :face (if (doom-modeline--active)
                                  'all-the-icons-red
                                'mode-line-inactive)
                        :v-adjust 0.02)))

(defun doom-modeline-update-pdf-pages ()
  "Update PDF pages."
  (setq doom-modeline--pdf-pages
        (let ((current-page-str (number-to-string (eval `(pdf-view-current-page))))
              (total-page-str (number-to-string (pdf-cache-number-of-pages))))
          (concat
            (propertize
              (concat (make-string (- (length total-page-str) (length current-page-str)) 32)
                    " P" current-page-str)
              'face 'mode-line)
            (propertize (concat "/" total-page-str) 'face 'doom-modeline-buffer-minor-mode)))))

(doom-modeline-def-segment pdf-pages
  "Display PDF pages."
  (if (doom-modeline--active) doom-modeline--pdf-pages
    (propertize doom-modeline--pdf-pages 'face 'mode-line-inactive)))

(doom-modeline-def-modeline 'pdf
  '(bar window-number pdf-pages pdf-icon buffer-name)
  '(misc-info matches major-mode process vcs))

#+end_src

*** Mood-line :ARCHIVE:
#+begin_src elisp :tangle packages.el
(package! mood-line)
#+end_src

#+begin_src emacs-lisp
(require 'mood-line)
(mood-line-mode)
#+end_src

**** Problema com Olivetti.
#+begin_src emacs-lisp
(defun mood-line--format (left right)
  "Return a string of `window-width' length containing LEFT and RIGHT, aligned respectively."
  (let ((reserve (length right)))
    (concat left
            " "
            (propertize " "
                        'display `((space :align-to (- right (- 0 right-margin) ,reserve))))
            right)))
#+end_src
    
*** Simple modeline :ARCHIVE:
#+begin_src elisp :tangle packages.el
(package! simple-modeline)
#+end_src

#+begin_src emacs-lisp
(require 'simple-modeline)

(defun my/simple-modeline-segment-modified ()
  "Displays a color-coded buffer modification/read-only indicator in the mode-line."
  (if buffer-file-name
      (let* ((read-only buffer-read-only)
             (modified (buffer-modified-p)))
        (if read-only
            (concat " "
                    (all-the-icons-octicon "lock" :face 'all-the-icons-red))
          (propertize
           (if modified " ‚óè" "  ")
           'face `(:inherit
                   ,(if modified 'simple-modeline-status-modified
                      (if read-only 'simple-modeline-status-error
                        'simple-modeline-unimportant))))))))

(setq simple-modeline-segments
      '((my/simple-modeline-segment-modified
         simple-modeline-segment-buffer-name
         simple-modeline-segment-position)
        (simple-modeline-segment-vc
         simple-modeline-segment-major-mode
         simple-modeline-segment-misc-info
         simple-modeline-segment-process)))
(simple-modeline-mode)
#+end_src

**** Problema com Olivetti
#+begin_src emacs-lisp
(defun simple-modeline--format (left-segments right-segments)
  "Return a string of `window-width' length containing LEFT-SEGMENTS and RIGHT-SEGMENTS, aligned respectively."
  (let* ((left (simple-modeline--format-segments left-segments))
         (right (simple-modeline--format-segments right-segments))
         (reserve (length right)))
    (concat
     left
     (propertize " "
                 'display `((space :align-to (- right (- 0 right-margin) ,reserve)))
                 'face '(:inherit simple-modeline-space))
     right)))
#+end_src

*** MLScroll (barra de rolagem na modeline) :ARCHIVE:
#+begin_src elisp :tangle packages.el
(package! mlscroll)
#+end_src

#+begin_src emacs-lisp
(use-package mlscroll
  :hook (server-after-make-frame . mlscroll-mode)
  :config
  (setq mlscroll-right-align nil))
#+end_src

** Divisores de janelas

#+begin_src emacs-lisp
(setq window-divider-default-bottom-width 2   ; default is 1
      window-divider-default-right-width  2)  ; default is 1
#+end_src

** Dashboard
*** Est√©tico

Pequenas modifica√ß√µes ao dashboard:

#+begin_src emacs-lisp
;; Desabilita o "benchmark"
(remove-hook 'window-setup-hook #'doom-display-benchmark-h)
#+end_src

*** Banner

#+begin_src emacs-lisp
(setq +doom-dashboard-functions '(doom-dashboard-widget-shortmenu
                                  doom-dashboard-widget-loaded))
#+end_src

A fonte padr√£o √© it√°lica e deixa os acrobatas tortos, vamos resolver isso:

#+begin_src emacs-lisp
(custom-set-faces!
  '(doom-dashboard-banner
    :inherit font-lock-comment-face
    :slant normal))
#+end_src

** Meu =zen-mode=

#+begin_src emacs-lisp
(defvar my/zen-enabled nil)

(defun my/zen-enable ()
  (interactive)
  (require 'org-starless)
  (hide-mode-line-mode +1)
  (org-starless-mode +1)
  (org-indent-mode -1)
  (setq-local my/zen-enabled t
              line-spacing 0.1
              display-line-numbers nil))

(defun my/zen-disable ()
  (interactive)
  (hide-mode-line-mode -1)
  (org-starless-mode +1)
  (org-indent-mode +1)
  (setq-local my/zen-enabled nil
              line-spacing 0
              display-line-numbers t))

(defun my/zen-toggle ()
  (interactive)
  (if my/zen-enabled
      (my/zen-disable)
    (my/zen-enable)))

(map! :leader "t z" #'my/zen-toggle)
#+end_src

* Minha biblioteca
** Pasta-bibioteca

Com algumas bibliotecas externas, por exemplo.

#+begin_src emacs-lisp
(let ((default-directory "~/.doom.d/lisp/lib"))
  (normal-top-level-add-subdirs-to-load-path))
(add-load-path! "lisp/lib")
#+end_src

** Fun√ß√µes (n√£o interativas)
*** Conselhos

Para desabilitar as mensagens chatas:

#+begin_src emacs-lisp
(defun advice--inhibit-message (f &rest r) (let ((inhibit-message t)) (apply f r)))
#+end_src

*** Checa se objeto √© uma lista de strings
#+begin_src emacs-lisp
(defun string-list-p (x) (and (listp x) (--all? (stringp it) x)))
#+end_src

** Fun√ß√µes (interativas)
*** Excluir todos os conselhos
#+begin_src emacs-lisp
(defun advice-unadvice (sym)
  "Remove all advices from symbol SYM."
  (interactive "aFunction symbol: ")
  (advice-mapc (lambda (advice _props) (advice-remove sym advice)) sym))
#+end_src

* Emacs em geral
** Scroll
#+begin_src emacs-lisp
(setq mouse-wheel-scroll-amount '(1 ((shift) . 1)) ;; one line at a time
      mouse-wheel-progressive-speed nil ;; don't accelerate scrolling
      scroll-margin 4
      scroll-step 1) ;; keyboard scroll one line at a time
#+end_src

** Edi√ß√£o de texto
*** Vari√°veis
#+begin_src emacs-lisp
(setq-default fill-column 80)

(setq amalgamating-undo-limit 1)

(setq gcmh-idle-delay 5)

(setq company-idle-delay 0.01
      company-minimum-prefix-length 4)

(setq mouse-drag-and-drop-region t
      mouse-drag-and-drop-region-cut-when-buffers-differ t
      mouse-drag-and-drop-region-show-tooltip nil)

(setq default-input-method "TeX")
#+end_src

Deixa o =text-scale-mode= mais devagar.

#+begin_src emacs-lisp
(setq text-scale-mode-step 1.05)
#+end_src
*** Salvando
Desabilita a mensagem de salvamento.

#+begin_src emacs-lisp
(advice-add 'save-buffer :around #'advice--inhibit-message)
#+end_src

*** Piscar o cursor :ARCHIVE:
#+begin_src emacs-lisp
(blink-cursor-mode +1)
#+end_src
*** Abbrev
Meu deus, como eu passei tanto tempo (4 meses) sem saber da exist√™ncia
desta coisa MARAVILHOSA??? √â INCR√çVEL!!!!

#+begin_src emacs-lisp
(add-hook! 'text-mode-hook
           (abbrev-mode +1))

(setq abbrev-file-name (concat doom-private-dir "abbrev_defs"))
#+end_src

** Pesquisa & regex
#+begin_src emacs-lisp
(pcre-mode +1)
#+end_src

** Menu de contexto

Retirado de:
[[https://somecallmespace.com/emacs-context-menu.html][some call me Space | Emacs Context Menu]]

#+begin_src emacs-lisp
(require 'context-menu)
(map! [mouse-3] 'my-context-menu)
#+end_src

** Popups
#+begin_src emacs-lisp
(setq +popup-defauts
      '(:side bottom
        :height 0.3
        :width 130
        :quit t
        :select ignore
        :ttl 5))

(setq +popup-default-alist
      '((window-height . 0.3)
        (reusable-frames . visible)))

#+end_src

** Outros :completion:

#+begin_src emacs-lisp
(remove-hook! '(org-mode-hook text-mode-hook) #'flyspell-mode)

(setq vterm-shell "zsh"
      delete-by-moving-to-trash t
      mouse-autoselect-window nil)
#+end_src

** Maximizar a janela?

Maximizar a janela ao iniciar?

#+begin_src emacs-lisp
;; (add-to-list 'initial-frame-alist '(fullscreen . maximized))
#+end_src

* Configura√ß√µes
** TODO Use-packages (tempor√°rio)

Primeiro, alguns pacotes configurados com o =use-package=:

#+begin_src emacs-lisp
(use-package prettify-utils
  :after (org latex))

(use-package tree-sitter
  :after doom-first-file-hook
  :config
  (require 'tree-sitter-langs)
  (global-tree-sitter-mode)
  (add-hook 'tree-sitter-after-on-hook #'tree-sitter-hl-mode))

(use-package scroll-on-drag
  :bind ([down-mouse-2] . #'scroll-on-drag))

(defun yas-get-snippet (mode key)
       (yas--fetch (yas--get-snippet-tables mode) key))

(use-package laas
  :commands (laas-mode))

(use-package lean4-mode
  :commands (lean4-mode))

(use-package mamimo
  :hook ((org-mode latex-mode markdown-mode) . mamimo-mode))
#+end_src

** TODO Packages.el (tempor√°rio)

Isso √© tempor√°rio, enquanto eu n√£o organizo o meu antigo arquivo =packages.el=.
#+begin_src emacs-lisp :tangle packages.el
;; -*- no-byte-compile: t; -*-
;;; $DOOMDIR/packages.el

;; latex
;; (package! latex-preview-pane)
(package! aas :recipe (:host github :repo "ymarco/auto-activating-snippets"))

;; doom
(package! writegood-mode :disable t)
(package! hl-line :disable t)
(package! smooth-scrolling)
(package! scroll-on-drag)
(package! nyan-mode)

;; Tree-sitter
(package! tree-sitter)
(package! tree-sitter-langs)

(package! lsp-treemacs)

(unpin! lsp-mode)
;; (unpin! consult)
(unpin! doom-themes)
(unpin! vertico)
(unpin! treemacs)

(package! benchmark-init :recipe (:host github :repo "kekeimiku/benchmark-init-el"))

(unpin! evil-tex)
(unpin! company-math)
(package! company-math)
(package! math-symbol-lists)
(package! company-math)
(package! real-auto-save)
#+end_src

** Major modes
*** Org
**** Vari√°veis
#+begin_src emacs-lisp
(setq org-directory "~/Lucas/org"
      org-attach-id-dir "data/"
      org-startup-folded t
      org-latex-packages-alist '(("" "tikz" t) ("" "tikz-cd" t))
      org-support-shift-select t
      org-hide-emphasis-markers nil
      org-latex-pdf-process '("%latex %f")
      org-latex-compilers '("tectonic" "pdflatex" "xelatex" "lualatex")
      org-latex-compiler "tectonic"
      org-src-window-setup 'plain
      org-highlight-latex-and-related '(native script)
      org-emphasis-regexp-components '("-[:space:]('\"{" "-[:space:].,:!?;'\")}\\[" "{}*[:space:]" "." 1)
      org-indent-indentation-per-level 1)
#+end_src

**** Gutter

O =git-gutter= n√£o funciona bem com o =org-indent-mode=:

#+begin_src emacs-lisp
(push 'org-mode git-gutter:disabled-modes)
#+end_src

**** Configura√ß√£o
#+begin_src elisp :tangle tangle/major/org.el :noweb yes
;;; tangle/major/org.el -*- lexical-binding: t; -*-
(require 'org-src)
(add-to-list 'org-src-block-faces '("latex" (:inherit default :extend t)))

(add-hook! org-mode
    <<org-mode-hook>>
    )

(set-popup-rule! "\*Org Src .+\*"
  :size 0.5)

(setq org-preview-latex-default-process 'dvisvgm)
(plist-put org-format-latex-options :scale 1.3)

(add-to-list
 '+company-backend-alist
 '(org-mode company-math-symbols-latex))

(map! :map 'evil-org-mode-map
      :i "C-l" #'flyspell-correct-move)

(after! ox-latex
  (add-to-list 'org-latex-classes
            '("report-noparts"
                "\\documentclass{report}"
                ("\\chapter{%s}" . "\\chapter*{%s}")
                ("\\section{%s}" . "\\section*{%s}")
                ("\\subsection{%s}" . "\\subsection*{%s}")
                ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                ("\\paragraph{%s}" . "\\paragraph*{%s}")
                ("\\subparagraph{%s}" . "\\subparagraph*{%s}"))))

(map! :mode 'org-mode :g "C-S-s" #'org-latex-export-to-pdf)
#+end_src

**** Hook

#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-hook
(auto-fill-mode +1)
(setq-local real-auto-save-interval 0.2)
(ws-butler-mode -1)
#+end_src

**** Esconder =:properties:=

Roubado do manual do Roam v2 (ou algo assim).

#+begin_src elisp :tangle tangle/major/org.el
(defun my/org-hide-properties ()
  "Hide all org-mode headline property drawers in buffer. Could be
slow if it has a lot of overlays."
  (interactive)
  (save-excursion
    (goto-char (point-min))
    (while (re-search-forward
            "^ *:properties:\n\\( *:.+?:.*\n\\)+ *:end:\n" nil t)
      (let ((ov_this (make-overlay (match-beginning 0) (match-end 0))))
        (overlay-put ov_this 'display "")
        (overlay-put ov_this 'hidden-prop-drawer t))))
  (put 'org-toggle-properties-hide-state 'state 'hidden))

(defun my/org-show-properties ()
  "Show all org-mode property drawers hidden by org-hide-properties."
  (interactive)
  (remove-overlays (point-min) (point-max) 'hidden-prop-drawer t)
  (put 'org-toggle-properties-hide-state 'state 'shown))

(defun my/org-toggle-properties ()
  "Toggle visibility of property drawers."
  (interactive)
  (if (eq (get 'org-toggle-properties-hide-state 'state) 'hidden)
      (org-show-properties)
    (org-hide-properties)))
#+end_src

#+begin_src emacs-lisp :tangle no :noweb-ref org-mode-hook
(my/org-hide-properties)
#+end_src
*** Haskell
#+begin_src emacs-lisp
(setq lsp-haskell-server-path "~/.local/bin/haskell-language-server-wrapper")
#+end_src

** Features
*** Ispell

#+begin_src emacs-lisp
(setq ispell-dictionary "pt_BR,en_US"
      ispell-personal-dictionary (concat doom-private-dir ".hunspell-personal"))

(unless (file-exists-p ispell-personal-dictionary)
  (write-region "" nil ispell-personal-dictionary nil 0))
#+end_src

Cria um dicion√°rio multilinguagem com portugu√™s e ingl√™s.

#+begin_src elisp
(after! ispell
  (ispell-hunspell-add-multi-dic "pt_BR,en_US")
  (ispell-set-spellchecker-params))
#+end_src
*** Orderless
Tamb√©m quero que tenha inicialismos (por exemplo, hmlm -> hide-mode-line-mode):
#+begin_src emacs-lisp
(setq orderless-matching-styles
      '(orderless-initialism
        orderless-literal
        orderless-regexp))
#+end_src
*** Org
**** Org-roam
#+begin_src emacs-lisp
(setq org-roam-directory "~/Lucas/notas"
      +org-roam-open-buffer-on-find-file nil)
#+end_src

#+begin_src elisp :tangle tangle/feature/org-roam.el
;;; tangle/feature/org-roam.el -*- lexical-binding: t; -*-

(defadvice! +org-roam-reuse-windows (&rest r)
  :before #'org-roam-preview-visit
  :before #'org-roam-node-visit
  (when org-roam-buffer-current-node
    (let ((window (get-buffer-window
                    (get-file-buffer
                      (org-roam-node-file org-roam-buffer-current-node)))))
      (when window (select-window window)))))

(defadvice! doom-modeline--buffer-file-name-roam-aware-a (orig-fun)
  :around #'doom-modeline-buffer-file-name ; takes no args
  (if (s-contains-p (expand-file-name org-roam-directory) (or buffer-file-name ""))
      (replace-regexp-in-string
       "\\(?:^\\|.*/\\)\\([0-9]\\{4\\}\\)\\([0-9]\\{2\\}\\)\\([0-9]\\{2\\}\\)[0-9]*-"
       "ü¢î(\\1-\\2-\\3) "
       (subst-char-in-string ?_ ?  buffer-file-name))
    (funcall orig-fun)))
#+end_src

**** Org-roam-ui
#+begin_src elisp :tangle packages.el
(package! websocket)
(package! org-roam-ui :recipe (:host github :repo "org-roam/org-roam-ui" :files ("*.el" "out")))
#+end_src

#+begin_src emacs-lisp
(use-package! websocket
    :after org-roam)

(use-package! org-roam-ui
    :after org-roam ;; or :after org
    :config
    (setq org-roam-ui-sync-theme t
          org-roam-ui-follow t
          org-roam-ui-update-on-save t))
#+end_src

**** TODO Org-ql
#+begin_src emacs-lisp :tangle packages.el
(package! org-ql)
#+end_src

**** Org-sidebar :ARCHIVE:
#+begin_src emacs-lisp :tangle packages.el
(package! org-sidebar)
#+end_src

#+begin_src emacs-lisp
(after! org-sidebar
  (defun :org-sidebar-refresh ()
    (interactive)
    (let* ((source-buffer (current-buffer))
           (sidebar-window (--first (window-parameter it 'org-sidebar-window)
                                    (window-at-side-list nil org-sidebar-side))))
      (when sidebar-window
        (with-current-buffer (window-buffer sidebar-window)
          (when (eq org-sidebar-source-buffer source-buffer)
            (org-sidebar-refresh)))))))
#+end_src
.
*** Projectile
#+begin_src emacs-lisp
(after! projectile
    (projectile-register-project-type 'julia '("Project.toml")
                                    :project-file "Project.toml"
                                    :test "julia -e \"using Pkg; Pkg.test()\""))
#+end_src
*** Temas
#+begin_src elisp :tangle packages.el
(package! seoul256-theme)
#+end_src


*** Treemacs
**** Ignorar (esconder) algumas coisas

Roubado do tecosaur.

#+begin_src emacs-lisp
(defcustom treemacs-file-ignore-extensions
  '("aux" "ptc" "fdb_latexmk" "fls" "synctex.gz" "toc"         ;; LaTeX
    "glg"  "glo"  "gls"  "glsdefs"  "ist"  "acn"  "acr"  "alg" ;; LaTeX - glossary
    "mw"                                                       ;; LaTeX - pgfplots
    "pdfa.xmpi")                                               ;; LaTeX - pdfx
  "File extension which `treemacs-ignore-filter' will ensure are ignored"
  :safe #'string-list-p)

(defcustom treemacs-file-ignore-globs
  '("*/_minted-*"                                        ;; LaTeX
     "*/.auctex-auto" "*/_region_.log" "*/_region_.tex") ;; AucTeX
  "Globs which will are transformed to `treemacs-file-ignore-regexps'
which `treemacs-ignore-filter' will ensure are ignored"
  :safe #'string-list-p)
#+end_src

**** Fringe only appears with width > 5
#+begin_src emacs-lisp
(setq doom-themes-treemacs-bitmap-indicator-width 8)
#+end_src

** Minor modes
*** Centaur-tabs :ARCHIVE:

#+begin_src elisp :tangle tangle/minor/centaur-tabs.el
;;; tangle/minor/centaur-tabs.el -*- lexical-binding: t; -*-
(defun centaur-tabs-buffer-groups ()
  "`centaur-tabs-buffer-groups' control buffers' group rules.

    Group centaur-tabs with mode if buffer is derived from `eshell-mode' `emacs-lisp-mode' `dired-mode' `org-mode' `magit-mode'.
    All buffer name start with * will group to \"Emacs\".
    Other buffer group by `centaur-tabs-get-group-name' with project name."
  (list
   (cond
    ((derived-mode-p 'prog-mode)
     "Editing")
    ((derived-mode-p 'dired-mode)
     "Dired")
    ((memq major-mode '(helpful-mode
                        help-mode))
     "Help")
    ((memq major-mode '(org-mode
                        org-agenda-clockreport-mode
                        org-src-mode
                        org-agenda-mode
                        org-beamer-mode
                        org-indent-mode
                        org-bullets-mode
                        org-cdlatex-mode
                        org-agenda-log-mode
                        diary-mode))
     "OrgMode")
    ((memq major-mode '(vterm-mode
                        term-mode
                        julia-repl-mode))
     "TermMode")
    ((or (string-equal "*" (substring (buffer-name) 0 1))
         (memq major-mode '(magit-process-mode
                            magit-status-mode
                            magit-diff-mode
                            magit-log-mode
                            magit-file-mode
                            magit-blob-mode
                            magit-blame-mode)))
     "Emacs")
    (t
     (centaur-tabs-get-group-name (current-buffer))))))


(setq centaur-tabs-style "bar"
      centaur-tabs-set-bar nil
      centaur-tabs-height 36
      centaur-tabs-plain-icons t
      centaur-tabs-label-fixed-length 10)

(after! centaur-tabs
  (centaur-tabs-group-by-projectile-project))
#+end_src
 
*** Company
**** Company-box # ainda n√£o sei se precisa :ARCHIVE:

#+begin_src elisp :tangle tangle/minor/company-box.el
;;; tangle/minor/company-box.el -*- lexical-binding: t; -*-
(defadvice! my/company-box-ensure-window-exists (&rest _)
  :before #'company-box--move-selection
  (unless (get-buffer-window (company-box--get-buffer) t)
    (company-box--set-frame (company-box--get-frame)))) ; or company-box--make-frame ?
#+end_src

*** Elcord

#+begin_src elisp :tangle packages.el
(package! elcord)
#+end_src

#+begin_src emacs-lisp
(setq elcord-editor-icon "emacs_icon"
      elcord-display-elapsed nil
      elcord--editor-name "Emacs"
      elcord-use-major-mode-as-main-icon t)
#+end_src

*** Evil
**** Vari√°veis
#+begin_src emacs-lisp
(setq evil-shift-round nil
      evil-cross-lines t

      ;; Respeita linhas visuais
      evil-respect-visual-line-mode t

      ;; Substitui v√°rios matches por linha no evil-ex
      evil-ex-substitute-global t)
#+end_src

**** FIXME Shift (n√£o funciona üòï)
Eu /realmente/ quero que o shift tenha 2 espa√ßos na linguagem elisp.
#+begin_src elisp :tangle tangle/minor/evil.el
;;; tangle/minor/evil.el -*- lexical-binding: t; -*-
(defadvice! ~evil-shift-width-elisp-advice (fun &rest r)
  :around #'evil-shift-width-elisp-advice
  (if (eq major-mode 'emacs-lisp-mode)
      (setq evil-shift-width 2)
    (apply fun r)))
#+end_src

**** Evil-mc
Multicursores com o Ctrl + clique esquerdo do mouse (como no VSCode).
#+begin_src elisp :tangle tangle/minor/evil.el
(defun evil-mc/toggle-cursor-on-click (event)
  "Add a cursor where you click, or remove a fake cursor that is
already there."
  (interactive "e")
  (mouse-minibuffer-check event)
  (require 'evil-mc)
  ;; Use event-end in case called from mouse-drag-region.
  ;; If EVENT is a click, event-end and event-start give same value.
  (let ((position (event-end event)))
    (if (not (windowp (posn-window position)))
        (error "Position not in text area of window"))
    (select-window (posn-window position))
    (let ((pt (posn-point position)))
      (if (numberp pt)
          ;; is there a fake cursor with the actual *point* right where we are?
          (unless (evil-mc-undo-cursor-at-pos pt)
            (save-excursion
              (goto-char pt)
              (evil-mc-make-cursor-here)))))))
(map! "C-<down-mouse-1>" nil)
(map! "C-<mouse-1>" #'evil-mc/toggle-cursor-on-click)
#+end_src

**** Objetos de texto
***** Org headlines
#+begin_src elisp :tangle tangle/minor/evil.el
(defun evil-org--parse-headline ()
  (save-excursion
    (end-of-line)
    (outline-previous-heading)
    (skip-chars-forward "* \t")
    (let* ((todo-start     (point))
           (todo-end1      (and org-todo-regexp
                                (let (case-fold-search) (looking-at (concat org-todo-regexp " ")))
                                (goto-char (1- (match-end 0)))))
           (todo-end2      (when todo-end1 (skip-chars-forward " \t") (point)))
           (priority-start (point))
           (priority-end   (when (looking-at "\\[#.\\][ \t]*") (goto-char (match-end 0))))
           (_              (and (let (case-fold-search) (looking-at org-comment-string))
                                (goto-char (match-end 0))))
           (title-start    (point))
           (tags-start     (when (re-search-forward "[ \t]+\\(:[[:alnum:]_@#%:]+:\\)[ \t]*$"
                                                    (line-end-position) 'move)
                             (goto-char (match-beginning 0))
                             (match-beginning 1)))
           (title-end      (point)))
      (list todo-start todo-end1 todo-end2 priority-start
            priority-end title-start title-end
            tags-start (line-end-position)))))

(evil-define-text-object evil-org-headline (count &optional beg end type)
  "Select the current org heading" :jump t
  (save-excursion
    (end-of-line)
    (outline-previous-heading)
    (list (line-beginning-position) (line-end-position))))

(evil-define-text-object evil-org-headline-title (c &rest _)
  "Select the title text in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (list (nth 5 parse) (nth 6 parse))))

(evil-define-text-object evil-org-headline-todo (c &rest _)
  "Select the todo entry in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (list (nth 0 parse) (nth 2 parse))))

(evil-define-text-object evil-org-headline-inner-todo (c &rest _)
  "Select the inner todo entry in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (list (nth 0 parse) (nth 1 parse))))

(evil-define-text-object evil-org-headline-priority (c &rest _)
  "Select the priority entry in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (list (nth 3 parse) (nth 4 parse))))

(evil-define-text-object evil-org-headline-tags (c &rest _)
  "Select the tags in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (list (nth 6 parse) (nth 8 parse))))

(evil-define-text-object evil-org-headline-inner-priority (c &rest r)
  "Select the inner part of priority in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (when (nth 4 parse)
      (let ((p (+ 2 (nth 3 parse)))) (list p (1+ p))))))

(evil-define-text-object evil-org-headline-inner-tags (c &rest _)
  "Select the inner part of tags in the current org heading" :jump t
  (let ((parse (evil-org--parse-headline)))
    (when (nth 7 parse)
      (list (1+ (nth 7 parse)) (1- (nth 8 parse))))))

(map! :map 'evil-inner-text-objects-map
      "h h" #'evil-org-headline-title
      "h t" #'evil-org-headline-inner-todo
      "h p" #'evil-org-headline-inner-priority
      "h a" #'evil-org-headline-inner-tags)

(map! :map 'evil-outer-text-objects-map
      "h h" #'evil-org-headline
      "h t" #'evil-org-headline-todo
      "h p" #'evil-org-headline-priority
      "h a" #'evil-org-headline-tags)
#+end_src

*** Flyspell

Vamos deix√°-lo menos pregui√ßoso: @performance

#+begin_src emacs-lisp
(setq flyspell-lazy-idle-seconds 0.4)
#+end_src

Com o =flyspell-correct=, vamos adicionar alguns atalhos:

#+begin_src emacs-lisp
(map! :ni "C-." #'flyspell-correct-move)
#+end_src
*** Focus
#+begin_src emacs-lisp
(setq focus-fraction 0.7)
(custom-set-faces!
  '(focus-unfocused :inherit custom-comment-tag :foreground "gray"))
#+end_src

*** Iedit

Desativa uma mensagem chata quando apertamos =M-d=

#+begin_src emacs-lisp
(setq iedit-toggle-key-default nil)
#+end_src

*** Lisps
**** Parinfer

 - =paren= Mode gives you full control of parens, while Parinfer corrects
   indentation. You can still adjust indentation, but you won't be able to
   indent/dedent past certain boundaries set by parens on previous lines.
   
 - =indent= Mode gives you full control of indentation, while Parinfer
   corrects or inserts close-parens where appropriate. Specifically, it only
   touches the groups of close-parens at the end of each line.

 - =smart= Mode is like Indent Mode, but it tries to preserve the structure too.
   
NOTE TO SELF: =smart= and =indent= won't allow inserting unmached }

#+begin_src emacs-lisp
(setq parinfer-rust-preferred-mode "indent")
#+end_src

*** Mamimo

#+begin_src emacs-lisp
(setq mamimo-greek-abbrevs-prefix ";")
(add-hook! 'mamimo-mode-hook
  (evil-tex-mode +1))
#+end_src

*** Mixed-pitch

#+begin_src emacs-lisp
(defface my-mixed-pitch-face
  '((t :family "Overpass" :weight light))
  "Face for `mixed-pitch-mode'")
(setq mixed-pitch-face 'my-mixed-pitch-face
      mixed-pitch-set-height nil)
#+end_src

*** Org
**** Org-appear :ARCHIVE:

Faz os elementos de markup desaparecerem automaticamente.

#+begin_src emacs-lisp :tangle packages.el
(package! org-appear)
#+end_src

#+begin_src emacs-lisp
(use-package org-appear
  :hook (org-mode . org-appear-mode)
  :config
  (setq org-appear-autolinks nil))
#+end_src

*** Vertico :completion:

#+begin_src emacs-lisp :tangle tangle/minor/vertico.el
(vertico-reverse-mode +1)
(setq vertico-resize t
      vertico-count 10)
#+end_src
*** Yasnippet
**** Movimentos entre campos
Uma dessas coisas que se imagina, por que n√£o fizeram assim?

#+begin_src elisp :tangle tangle/minor/yasnippet.el
;;; tangle/minor/yasnippet.el -*- lexical-binding: t; -*-
(defun yas--maybe-move-to-active-field (snippet)
  "Try to move to SNIPPET's active (or first) field and return it if found."
  (let ((target-field (or (yas--snippet-active-field snippet)
                          (car (yas--snippet-fields snippet)))))
    (when target-field
      (yas--move-to-field snippet target-field)
      (goto-char (yas--field-end target-field))
      target-field)))
#+end_src

** Carregar
O trecho a seguir carrega os arquivos de configura√ß√µes nos diret√≥rios
=lisp/major= e =lisp/minor=, e faz com que cada arquivo seja carregado apenas
depois do m√≥dulo de mesmo nome ser carregado (com a fun√ß√£o =after!=).
#+begin_src emacs-lisp
(dolist (type '(major minor features))
  (let ((folder (format "~/.doom.d/lisp/%s/" type)))
    (dolist (file (file-expand-wildcards (concat folder "*.el")))
      (let ((f (file-name-sans-extension (file-name-nondirectory file))))
        (eval `(after! ,(intern f) (load! ,f ,folder)))))))
#+end_src

#+begin_src emacs-lisp
(dolist (type '(major minor features))
  (let ((folder (format "%stangle/%s/" doom-private-dir type)))
    (dolist (file (file-expand-wildcards (concat folder "*.el")))
      (let ((f (file-name-sans-extension (file-name-nondirectory file))))
        (eval `(after! ,(intern f) (load! ,f ,folder)))))))
#+end_src

* Atalhos de teclado
** Fazer o binding =SPC s I= abrir a sess√£o com narrowing
#+begin_src emacs-lisp
(map! :mode 'org-mode
      :map 'doom-leader-search-map
      "I" (cmd! (funcall-interactively (key-binding " si"))
                (org-tree-to-indirect-buffer)))
#+end_src

** Comandos familiares

Porque ningu√©m merece tantos atalhos diferentes.

#+begin_src emacs-lisp
;; (map! "C-S-s" 'isearch-forward)
(map! :egni "C-s" 'save-buffer)
(map! :egni "C-/" 'evilnc-comment-or-uncomment-lines)

(map! :i "C-v" 'yank)
(map! :i "C-z" 'evil-undo)
(map! :i "C-S-z" 'evil-redo)
(map! :i "C-x" 'evil-delete)
#+end_src

** Linhas visuais
#+begin_src emacs-lisp
(map! :map evil-motion-state-map
      "j" 'evil-next-visual-line
      "k" 'evil-previous-visual-line
      "<down>" 'evil-next-visual-line
      "<up>" 'evil-previous-visual-line)
#+end_src

** Evil ABNT :ARCHIVE:

Nossos teclados t√™m uma tecla a mais no melhor lugar poss√≠vel, n√£o podemos
desperdi√ßar a oportunidade de usar =jkl√ß=:

#+begin_src emacs-lisp :tangle no
;; no dia em que eu precisar usar teclado americano, eu vou me arrepender...

(map! :map minibuffer-local-map
      "C-k" #'next-line
      "C-l" #'previous-line
      :i "C-k" #'next-line
      :i "C-l" #'previous-line)

(map! :map evil-motion-state-map
      "j" 'evil-backward-char
      "k" 'evil-next-visual-line
      "l" 'evil-previous-visual-line
      "√ß" 'evil-forward-char)

(map! :map evil-window-map
      ;; Navigation
      "j"       #'evil-window-left
      "k"       #'evil-window-down
      "l"       #'evil-window-up
      "√ß"       #'evil-window-right
      "C-j"     #'evil-window-left
      "C-k"     #'evil-window-down
      "C-l"     #'evil-window-up
      "C-√ß"     #'evil-window-right
      ;; Swapping windows
      "J"       #'+evil/window-move-left
      "K"       #'+evil/window-move-down
      "L"       #'+evil/window-move-up
      "√á"       #'+evil/window-move-right)

(map! :i "M-J" 'evil-backward-char
      :i "M-K" 'evil-next-line
      :i "M-L" 'evil-previous-line
      :i "M-√á" 'evil-forward-char)

(after! treemacs (evil-define-key 'treemacs treemacs-mode-map "l" nil "h" nil))

;; (evil-define-key '(visual normal) Info-mode-map "l" nil)
(map! :map Info-mode-map :vn "l" nil)

(map! :after treemacs
      :map evil-treemacs-state-map
      "j"      #'treemacs-COLLAPSE-action
      "k"      #'treemacs-next-line
      "l"      #'treemacs-previous-line
      "√ß"      #'treemacs-RET-action)
#+end_src

** Hydras
Uma hist√≥ria antiga.

N√£o gosto do estilo do pop up
#+begin_src emacs-lisp
(setq hydra-is-helpful nil)
#+end_src

Tamanho da janela
#+begin_src emacs-lisp
(defhydra window-height-hydra (evil-window-map)
  "window height"
  ("=" evil-window-increase-height "")
  ("-" evil-window-decrease-height "")
  (">" evil-window-increase-width "")
  ("<" evil-window-decrease-width ""))

(defhydra workspace-hydra (doom-leader-workspace-map)
  "workspace"
  ("]" +workspace/switch-right "")
  ("[" +workspace/switch-left "")
  ("}" +workspace/swap-right "")
  ("{" +workspace/swap-left ""))
#+end_src

** Kitty (Terminal)
#+begin_src emacs-lisp
(map! :prefix-map ("\x80" . "kitty C map")
      :map 'key-translation-map
      "/" "C-/")

(map! :prefix-map ("\x81" . "kitty C-S map")
      :map 'key-translation-map
      "z" (kbd "C-S-z"))
#+end_src

** Edit leader key
#+begin_src emacs-lisp
(map! :leader
      :prefix ("e" . "edit")
      :desc "New snipet" "s" #'+snippets/new
      :desc "New alias" "a" #'+snippets/new-alias)

(map! :i "C-M-x" ctl-x-map)
#+end_src

** Centralizar ao procurar com o evil
#+begin_src emacs-lisp
;; (evil-define-motion search-previous-and-recenter (count)
;;   :jump t
;;   :type exclusive
;;   (evil-ex-search-previous count)
;;   (call-interactively #'evil-scroll-line-to-center))

;; (map! :n [remap evil-ex-search-previous] #'search-previous-and-recenter)
#+end_src

** Outros

#+begin_src emacs-lisp
(map! "M-j" 'drag-stuff-down
      "M-k" 'drag-stuff-up)

(map! :map lean-mode-map "M-." 'lean-find-definition)

(map! :map TeX-mode-map "C-S-s" 'TeX-command-run-all)
#+end_src
